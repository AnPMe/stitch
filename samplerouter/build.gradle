apply plugin: 'com.android.library'

version = "1.0"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion



    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            //将所有module里的router文件夹都作为路由module的源码文件夹，方便Module开发时的方便
            ArrayList<String> strings = new ArrayList<String>()
            File[] modules = rootDir.listFiles(new FileFilter() {
                @Override
                boolean accept(File pathname) {
                    return (pathname.isDirectory()
                            && pathname.name != "gradle"
                            && pathname.name != "build"
                            && !pathname.name.startsWith("."))
                }
            })
            for (File f : modules) {
                strings.add(f.absolutePath + File.separator + "router")
            }
            //不要忘了把原始的源码目录添加进来
            strings.addAll(java.srcDirs)
            java.srcDirs = strings
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:26.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    implementation 'bamboo.component:stitcher:1.0'
}
